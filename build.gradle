plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "2.1.10"
    id "org.openjfx.javafxplugin" version "0.1.0"
}

ext {
    appTitle = "reviso"
    appTarget = "21"
    appVersion = "2.2.0"
}

allprojects {
    group = appTitle
    version = appVersion
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.github.ajalt.clikt:clikt:5.0.3"
    implementation "net.pearx.kasechange:kasechange-jvm:1.4.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.10"
}

tasks.register('setup') {
    ext {
        getPython = { ->
            if (!org.gradle.internal.os.OperatingSystem.current().isWindows()) {
                return "python3"
            }

            return "python"
        }
    }

    doLast {
        exec {
            executable getPython()
            args "setup.py", "--icons", "--wrapper"
        }
    }
}

application {
    mainModule = "${appTitle}"
    mainClass = "${appTitle}.Main"
}

build {
    dependsOn setup
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "${appTarget}"
    }
}

jar {
    manifest {
        attributes "Implementation-Title": appTitle
        attributes "Implementation-Version": appVersion
    }
}

java {
    targetCompatibility = "${appTarget}"
}

javafx {
    version = "21.0.6"
    modules = [
        "javafx.base",
        "javafx.controls",
        "javafx.fxml",
        "javafx.graphics",
    ]
}

startScripts {
    enabled = false
}
